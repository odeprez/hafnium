# Copyright 2021 The Hafnium Authors.
#
# Use of this source code is governed by a BSD-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/BSD-3-Clause.

import("//build/image/image.gni")
import("//test/hftest/args.gni")
import("//src/arch/aarch64/args.gni")

# VM that will control execution of tests in a VM-to-SP set-up
vm_kernel("vm_primary") {
  testonly = true
  public_configs = [
    "services:config",
    "//src/arch/aarch64:arch_config",
  ]

  sources = [
    "dir_msg.c",
    "notifications.c",
    "power_mgt.c",
    "setup_and_discovery.c",
  ]

  deps = [
    "//src/arch/${plat_arch}/hftest:power_mgmt",
    "//test/hftest:hftest_primary_vm",
    "//test/vmapi/common:common",
  ]
}

# SP that will be controlled via communication/scheduling primitives by the
# former SP
vm_kernel("secure_partitions_services") {
  testonly = true
  public_configs = [ "services:config" ]

  deps = [
    "services:message_loop",
    "//test/hftest:hftest_secure_service",
  ]
}

manifest("partition_manifest_service_sp_first") {
  source = "partition_manifest_service_sp_first.dts"
  output = "partition_manifest_service_sp_first.dtb"
}

manifest("partition_manifest_service_sp_second") {
  source = "partition_manifest_service_sp_second.dts"
  output = "partition_manifest_service_sp_second.dtb"
}

partition_package("service_sp_second_package") {
  testonly = true
  pm_offset = "0x1000"
  img_offset = "0x2000"
  files = [ [
        "partition_manifest_service_sp_second.dtb",
        "secure_partitions_services.bin",
        ":partition_manifest_service_sp_second",
        ":secure_partitions_services",
      ] ]
  output = "service_sp_second_package.img"
}

partition_package("service_sp_first_package") {
  testonly = true
  pm_offset = "0x1000"
  img_offset = "0x2000"
  files = [ [
        "partition_manifest_service_sp_first.dtb",
        "secure_partitions_services.bin",
        ":partition_manifest_service_sp_first",
        ":secure_partitions_services",
      ] ]
  output = "service_sp_first_package.img"
}

manifest("vm_primary_manifest") {
  source = "partition_manifest_nwd_primary.dts"
  output = "partition_manifest_nwd_primary.dtb"
}

partition_package("vm_primary_test_package") {
  testonly = true
  pm_offset = "0x1000"
  img_offset = "0x2000"
  files = [ [
        "partition_manifest_nwd_primary.dtb",
        "vm_primary.bin",
        ":vm_primary_manifest",
        ":vm_primary",
      ] ]
  output = "vm_primary_test_package.img"
}

vm_kernel("sel0_partition") {
  testonly = true

  deps = [
    "services:message_loop",
    "//test/hftest:hftest_sel0_partition_base",
  ]
}

manifest("partition_manifest_service_sp_second_el0") {
  source = "partition_manifest_service_sp_second_el0.dts"
  output = "partition_manifest_service_sp_second_el0.dtb"
}

partition_package("service_sp_second_el0_package") {
  testonly = true
  pm_offset = "0x1000"
  img_offset = "0x2000"
  files = [ [
        "partition_manifest_service_sp_second_el0.dtb",
        "sel0_partition.bin",
        ":partition_manifest_service_sp_second_el0",
        ":sel0_partition",
      ] ]
  output = "service_sp_second_el0_package.img"
}

partitions_json("ffa_both_world_partitions_vhe_test") {
  testonly = true

  sps = [
    [
      "service_sp_second_el0_package.img",
      "manifest_service_sp_second_el0.dts",
      ":service_sp_second_el0_package",
      ":partition_manifest_service_sp_second_el0",
    ],
    [
      "service_sp_first_package.img",
      "manifest_service_sp_first.dts",
      ":service_sp_first_package",
      ":partition_manifest_service_sp_first",
    ],
  ]

  vms = [ [
        "vm_primary_test_package.img",
        "manifest.dts",
        ":vm_primary_test_package",
        ":vm_primary_test_package",
      ] ]

  json_file = "ffa_both_world_partitions_vhe_test.json"
}

partitions_json("ffa_both_world_partitions_test") {
  testonly = true

  sps = [
    [
      "service_sp_second_package.img",
      "manifest_service_sp_second.dts",
      ":service_sp_second_package",
      ":partition_manifest_service_sp_second",
    ],
    [
      "service_sp_first_package.img",
      "manifest_service_sp_first.dts",
      ":service_sp_first_package",
      ":partition_manifest_service_sp_first",
    ],
  ]

  vms = [ [
        "vm_primary_test_package.img",
        "manifest.dts",
        ":vm_primary_test_package",
        ":vm_primary_test_package",
      ] ]

  json_file = "ffa_both_world_partitions_test.json"
}
